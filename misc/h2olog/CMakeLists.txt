CMAKE_MINIMUM_REQUIRED(VERSION 2.8.11)
CMAKE_POLICY(SET CMP0003 NEW)

ENABLE_LANGUAGE(CXX)
SET(CMAKE_CXX_STANDARD 11)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)
SET(CMAKE_CXX_EXTENSIONS OFF)

INCLUDE(GNUInstallDirs)

PROJECT(h2olog)

IF(NOT H2O_DIR)
    SET(H2O_DIR "${CMAKE_CURRENT_SOURCE_DIR}/../..")
ENDIF(NOT H2O_DIR)

IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE Release)
ENDIF(NOT CMAKE_BUILD_TYPE)

SET(CMAKE_CXX_FLAGS_RELEASE  "-O3 -flto")

SET(CMAKE_CXX_FLAGS "-Wall -Wextra -Wno-unused-parameter -g3 ${CMAKE_CXX_FLAGS}")

FIND_PATH(LIBBCC_INCLUDE_DIR NAMES bcc/BPF.h)
FIND_LIBRARY(LIBBCC_LIB "bcc")
IF (LIBBCC_LIB)
  IF (LIBBCC_INCLUDE_DIR)
    INCLUDE_DIRECTORIES("${LIBBCC_INCLUDE_DIR}")
  ELSE (LIBBCC_INCLUDE_DIR)
    MESSAGE(SEND_ERROR "libbcc header files not found")
  ENDIF (LIBBCC_INCLUDE_DIR)
ELSE (LIBBCC_LIB)
  MESSAGE(SEND_ERROR "libbcc not found")
ENDIF (LIBBCC_LIB)

SET(SOURCE_FILES
  "h2olog.h"
  "h2olog.cc"
  "http.cc"
  "json.h"
  "json.cc"
  "quic.h"
  "${H2O_DIR}/include/h2o/version.h"
)

FILE(COPY "${H2O_DIR}/deps/quicly/quicly-probes.d" DESTINATION ${CMAKE_CURRENT_BINARY_DIR})
LIST(APPEND DTRACE_FILES "${CMAKE_CURRENT_BINARY_DIR}/quicly-probes.d")

FILE(COPY "${H2O_DIR}/h2o-probes.d" DESTINATION ${CMAKE_CURRENT_BINARY_DIR})
LIST(APPEND DTRACE_FILES "${CMAKE_CURRENT_BINARY_DIR}/h2o-probes.d")

ADD_CUSTOM_COMMAND(
    OUTPUT  "${CMAKE_CURRENT_SOURCE_DIR}/generated-quic.cc"
    COMMAND "${CMAKE_CURRENT_SOURCE_DIR}/misc/gen-quic-bpf.py" "${CMAKE_CURRENT_BINARY_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}/generated-quic.cc"
    DEPENDS ${DTRACE_FILES}
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/misc/gen-quic-bpf.py"
)
LIST(APPEND SOURCE_FILES "${CMAKE_CURRENT_SOURCE_DIR}/generated-quic.cc")

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})
INCLUDE_DIRECTORIES("${H2O_DIR}/include")

ADD_EXECUTABLE(h2olog ${SOURCE_FILES})
TARGET_LINK_LIBRARIES(h2olog ${LIBBCC_LIB})

INSTALL(TARGETS h2olog RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})
